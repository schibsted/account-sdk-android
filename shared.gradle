// Git helpers
static def getGitVersionCode(git) {
    return git.log(includes: ['HEAD']).size()
}

static def getGitVersionName(git) {
    def tags = git.tag.list().sort {it.commit.time}

    if (tags.size() > 0) {
        return tags.sort {it.commit.time}.last().fullName.replace("refs/tags/", "").replaceAll("^v", "")
    } else {
        return "0.0.0"
    }
}

static def isSnapshot(git) {
    def head = git.head()
    def headTagMatch = git.tag.list().findAll { it.commit == head }

    return (headTagMatch == null || headTagMatch.empty)
}

// Dependency helpers
static def addDependency(dependenciesNode, String group, String name, String version) {
    def dependencyNode = dependenciesNode.appendNode('dependency')
    dependencyNode.appendNode('groupId',    group)
    dependencyNode.appendNode('artifactId', name)
    dependencyNode.appendNode('version',    version)
}

static def fixDependencies(node, project) {
    def confs = project.configurations
    (confs.api.allDependencies  + confs.implementation.allDependencies).each {
        if (it.version != null && it.version != 'unspecified') { // Don't add projects
            addDependency(node, it.group, it.name, it.version)
        }
    }
}

ext {
    getGitVersionCode = this.&getGitVersionCode
    getGitVersionName = this.&getGitVersionName
    addDependency = this.&addDependency
    fixDependencies = this.&fixDependencies
    isSnapshot = this.&isSnapshot
}
