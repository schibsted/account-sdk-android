apply plugin: 'org.jetbrains.kotlin.android.extensions'

android {
    buildTypes {
        release {
            consumerProguardFiles "core-rules.pro"
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    api project(':common')

    api "com.squareup.retrofit2:converter-gson:${libs.retrofit}"
    implementation "com.squareup.retrofit2:retrofit:${libs.retrofit}"
    implementation "com.android.support:support-annotations:${libs.support}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${libs.kotlin}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${libs.kotlin}"
    implementation "com.android.support:support-core-utils:${libs.support}"

    testImplementation "junit:junit:${libs.jUnit}"
    testImplementation "io.kotlintest:kotlintest:${libs.kotlintest}"
    testImplementation "com.nhaarman:mockito-kotlin:${libs.mockitoKotlin}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${libs.coroutines}"
}

bintrayUpload.dependsOn ':core:check', ':core:assembleRelease'

publishing {
    publications {
        core(MavenPublication) {
            artifactId 'account-sdk-android-core'
            groupId artifactOrg
            version artifactVersion

            artifact "$buildDir/outputs/aar/core-release.aar"
//            artifact(tasks.getByName("sourceJar"))
//            artifact(tasks.getByName("javadocJar"))

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                fixDependencies(dependenciesNode, project)
                addDependency(dependenciesNode, artifactOrg, "account-sdk-android-common", artifactVersion)
                addArtifactMetadata(asNode(), "Schibsted Account SDK Core Module",
                        "The core module for the Schibsted Account SDK", "https://github.com/schibsted/account-sdk-android", pomConfig)
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? bintrayApiKey : System.getenv('BINTRAY_API_KEY')
    publications = ['core']

    pkg {
        repo = 'Account-SDK-Android'
        name = 'Core'
        description = "Core module for the Schibsted Account SDK"
        userOrg = 'schibsted'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/schibsted/account-sdk-android.git'
        publish = true

        version {
            name = artifactVersion
            desc = "Account SDK Android Core $artifactVersion"
            released  = new Date()
            vcsTag = "v$artifactVersion"
        }
    }
}
